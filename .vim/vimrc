" ----------------------------------------------------------------------------
" ---------------------------- PLUGINS ---------------------------------------
if filereadable($CONFIG . "/plugins.vim")
	source $CONFIG/plugins.vim
else
	source /home/bryan/Dropbox/dotfiles/extras/plugins.vim
endif

filetype plugin indent on
filetype plugin on
runtime macros/matchit.vim

" ---------------------- GVim/Vim Specific Configs ---------------------------
if has("gui_running")
	colo desert
	cd $HOME
else
	set background=dark
endif

" -------------------------------- SETS --------------------------------------
" Status Line Settings (mode display)
set number
set laststatus=2
set clipboard=unnamedplus
set colorcolumn=100
set autoindent
set backspace=2
set tabstop=4
set shiftwidth=4
set expandtab
set nocompatible
" Allows Local .vimrc to be loaded
set exrc
set secure
" Enables Modeline Magic
set modeline
" Allows me to use the mouse
set mouse=a
" An attempt to fix the issue where mouse-clicks cause random output to screen
set ttymouse=sgr
" Allows me to highlight using a colon (:)
set iskeyword+=:
" Search Settings
set incsearch
set ignorecase
set smartcase
" Improves tab completion from normal mode
set wildmenu
set wildmode=full
" 'find'/ctrlp should ignore these folders
set wildignore+=*/__pycache__/*,*/.git/*,*/bin/*,*/venv/*
" Increase command history limit
set history=200
" Always show one line above/below cursor
set scrolloff=1
" Allows you to change buffers without first saving the current buffer
set hidden
" Change default program for :grep to ack
set grepprg=ack\ --nogroup\ --column\ $*
set grepformat=%f:%l:%c:%m
" Autocompletion will only recommend completions that match the typed case
set infercase
set listchars=eol:$,tab:>-,trail:~,extends:>,precedes:<,space:â£
set nolist
set cursorline
set cursorcolumn
" Indents wraps
set breakindent
" Disables search highlighting
set nohlsearch
" Changes location of viminfo file
set viminfo+=n~/.vim/viminfo

" --------------------------------- LETS -------------------------------------
" Appends the current working directory to PATH
let tempPath=expand("%:p:h") . "/**"
exec "set path+=" . tempPath
" Sets LaTeX as default for .tex files
let g:tex_flavor = "latex"
" The <Leader> key can be used for extra mappings
let mapleader = ","
noremap \ ,
" Sets minimum size of active split (by percentage)
" let &winwidth = &columns * 6 / 10
let &winheight = &lines * 6 / 10

" ---------------------------- SYNTAX ----------------------------------------
syntax on
syntax enable
colorscheme solarized
set background=dark
" Enables syntax highlighting to work properly for certain filetypes
au BufRead,BufNewFile *.nasm set filetype=nasm
au BufRead,BufNewFile *.txt set filetype=txt
au BufRead,BufNewFile *.xmobarrc set syntax=haskell
au BufRead,BufNewFile *.lshrc set syntax=zsh
au BufRead,BufNewFile *.conf set syntax=cfg
" Used to make syntax highlighting more readable when using Linux
" transparent terminal
hi Constant ctermfg=lightmagenta 
hi SpellBad cterm=underline
hi Normal guibg=NONE ctermbg=NONE

" ------------------------------ KEY MAPPINGS --------------------------------
" Enables 'very magic' mode by default
nnoremap / /\v
vnoremap / /\v
cnoremap %s/ %smagic/
cnoremap \>s/ \>smagic/
nnoremap :g/ :g/\v
nnoremap :g// :g//
" Preserves flags when repeating the last substitute command.
nnoremap & :&&<CR>
xnoremap & :&&<CR>
" Allows for visual search
xnoremap * :<C-u>call <SID>VSetSearch('/')<CR>/<C-R>=@/<CR><CR>
xnoremap # :<C-u>call <SID>VSetSearch('?')<CR>?<C-R>=@/<CR><CR>
" Allows space to work in Normal-Mode
nnoremap <space> i<space><esc>l
" Put space before and after character under cursor
nnoremap <Leader><space> a<space><esc>hi<space><esc>l
" NOTE:: Just use ZZ in normal mode to save and quit.
" Shortcuts for quiting vim
noremap <Leader>e :quit<CR>
" Shortcuts for saving vim
noremap <Leader>s :w<CR>
" Makes enter key work right in Normal mode
nnoremap <CR> o<Esc>
" Plugin Mappings
nmap <C-N> :cd %:h<CR>:NERDTree<CR><S-B>
nmap <Leader>p :CtrlPMRU<CR>
nmap <Leader>z :ZoomWin<CR>
nmap <C-g> :Gstatus<CR>
nmap <Leader>f :CtrlSF 
" Swap highlighted word with last word in buffer
vnoremap <Leader>x <Esc>`.``gvP``P
nmap <Leader>x ve<Leader>x
" Will execute the 'Run' command which varies based on the file type
nmap <Leader>r :w<CR>:Run<CR>
imap <Leader>r <Esc><Leader>r
nmap <Leader>R :w<CR>:Run2<CR>
imap <Leader>R <Esc><Leader>R
" Improve up/down movement on wrapped lines
nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k
" Sets tmux pane to the current directory
nnoremap <Leader><F12> :call VimuxRunCommand("cd " .expand("%:p:h") ."&& clear")<CR>
" Allow saving of files as sudo when I forgot to start vim using sudo.
cmap w!! w !sudo tee > /dev/null %
" Used to recall commands from history
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>
" mapping to wrap paragraph
nnoremap <Leader>g gqap
" deletes current file and buffer
nnoremap <C-Del> :call delete(expand('%'))<CR>
nnoremap <C-s> :SyntasticToggleMode<CR>
" Edit Snippet File
nnoremap <Leader>S :Ex ~/Dropbox/dotfiles/.vim/bundle/vim-snippets/UltiSnips<CR>
" Edit ftplugin File
nnoremap <Leader>F :Ex ~/Dropbox/dotfiles/.vim/ftplugin<CR>
" Delete Buffer
nnoremap <Leader>d :b#<bar>sp<bar>b#<bar>bd<CR>
" Vertical Buffer
nmap <Leader>v :vert sb
imap <Leader>v <Esc><Leader>v
" Omnicompletion shortcut
inoremap <C-o> <C-x><C-o>
" Toggles LiveDown for Markdown (e.g. README.md)
nmap gm :LivedownToggle<CR>
" Pulls Up Documentation for Module/Function Under Cursor
nnoremap <Leader>l yiW:Silent LangDoc --ext=<C-r>=expand('%:e')<CR> -m '<C-r>"'<CR>
nnoremap <Leader>L yW:Silent LangDoc --ext=<C-r>=expand('%:e')<CR> -m '<C-r>"'<CR>
command! -nargs=1 Doc execute "Silent LangDoc --ext=" . expand('%:e') . " -m <args>"
" Maps command to 'tm' shell script
command! -nargs=1 Tm execute "Silent tm <args>"
imap <C-h> <Esc><C-h>
imap <C-l> <Esc><C-l>
imap <C-j> <Esc><C-j>
imap <C-k> <Esc><C-k>

" -------------------------- SPLIT AND TAB SETTINGS --------------------------
" See: http://www.codeography.com/2013/06/19/navigating-vim-and-tmux-splits "
if exists('$TMUX')
  function! TmuxOrSplitSwitch(wincmd, tmuxdir)
    let previous_winnr = winnr()
    silent! execute "wincmd " . a:wincmd
    if previous_winnr == winnr()
      call system("tmux select-pane -" . a:tmuxdir)
      redraw!
    endif
  endfunction

  let previous_title = substitute(system("tmux display-message -p '#{pane_title}'"), '\n', '', '')
  let &t_ti = "\<Esc>]2;vim\<Esc>\\" . &t_ti
  let &t_te = "\<Esc>]2;". previous_title . "\<Esc>\\" . &t_te

  nnoremap <silent> <C-h> :call TmuxOrSplitSwitch('h', 'L')<cr>
  nnoremap <silent> <C-j> :call TmuxOrSplitSwitch('j', 'D')<cr>
  nnoremap <silent> <C-k> :call TmuxOrSplitSwitch('k', 'U')<cr>
  nnoremap <silent> <C-l> :call TmuxOrSplitSwitch('l', 'R')<cr>
else
  map <C-h> <C-w>h
  map <C-j> <C-w>j
  map <C-k> <C-w>k
  map <C-l> <C-w>l
endif

" ------------------------------ AUTOS ---------------------------------------
" Turns spellcheck on for certain file extensions
" 2nd line disables the colorcolumn
autocmd BufRead,BufNewFile *.txt set colorcolumn=
autocmd BufRead,BufNewFile *.md setlocal spell spelllang=en_us
autocmd BufRead,BufNewFile *.md set colorcolumn=
autocmd BufRead,BufNewFile *.html setlocal spell spelllang=en_us
autocmd BufRead,BufNewFile *.html set colorcolumn=
autocmd BufRead,BufNewFile *.tex setlocal spell spelllang=en_us
autocmd FileType mail setlocal spell spelllang=en_us
autocmd ColorScheme * highlight RedundantSpaces ctermbg=red
" Automatic rewriting of .vimrc
 autocmd! bufwritepost .vimrc source %
" http://stackoverflow.com/questions/16359878/vim-how-to-map-shift-enter
autocmd CmdwinEnter * nnoremap <CR> <CR>
autocmd BufReadPost quickfix nnoremap <CR> <CR>

" Detects mutt filetypes
augroup filetypedetect
  " Mail
  autocmd BufRead,BufNewFile *mutt-*			  setfiletype mail
augroup END

autocmd BufRead,BufNewFile *.tex,*.anki_vim let b:delimitMate_quotes = "\" ' $"
" Disables auto-comments on newlines
autocmd FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o
" Enables recognition of LocalAlias files
autocmd BufRead,BufNewFile .lshrc set ft=zsh

" ------------------------- FUNCTIONS AND COMMANDS ---------------------------
" Redraws screen after silent command
command! -nargs=1 Silent
\   execute 'silent !' . <q-args>
\ | execute 'redraw!'

let g:S = 0  "result in global variable S
function! Sum(number)
  let g:S = g:S + a:number
  return a:number
endfunction

function! s:VSetSearch(cmdtype)
	let temp = @s
	norm! gv"sy
	let @/ = '\V' . substitute(escape(@s, a:cmdtype.'\'), '\n', '\\n', 'g')
	let @s = temp
endfunction

" Automatically creates directory if it does not already exist
" https://stackoverflow.com/questions/4292733/vim-creating-parent-directories-on-save
function s:MkNonExDir(file, buf)
	if empty(getbufvar(a:buf, '&buftype')) && a:file!~#'\v^\w+\:\/'
		let dir=fnamemodify(a:file, ':h')
		if !isdirectory(dir)
			call mkdir(dir, 'p')
		endif
	endif
endfunction
augroup BWCCreateDir
	autocmd!
	autocmd BufWritePre * :call s:MkNonExDir(expand('<afile>'), +expand('<abuf>'))
augroup END

function! GitBranch()
  return system("git rev-parse --abbrev-ref HEAD 2>/dev/null | tr -d '\n'")
endfunction

function! StatuslineGit()
  let l:branchname = GitBranch()
  return strlen(l:branchname) > 0?'  ('.l:branchname.')  ':''
endfunction

" highlight VertSplit ctermfg=DarkGrey ctermbg=DarkGrey
highlight StatusLine cterm=NONE ctermfg=black ctermbg=yellow
highlight StatusLineNC cterm=NONE ctermfg=black ctermbg=DarkGrey

" highlight SLColor1 ctermfg=white ctermbg=DarkRed
" highlight SLColor2 ctermfg=black ctermbg=DarkGrey
" highlight Blank ctermfg=black ctermbg=white

set statusline=
set statusline+=%{StatuslineGit()}
set statusline+=\ %f
set statusline+=%m\ 
set statusline+=%=
set statusline+=\ %y
set statusline+=\ %{&fileencoding?&fileencoding:&encoding}
set statusline+=\[%{&fileformat}\]
set statusline+=\ %p%%
set statusline+=\ %l:%c
set statusline+=\ 
